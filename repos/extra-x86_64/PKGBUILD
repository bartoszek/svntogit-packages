# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kblog
pkgver=18.08.3
pkgrel=1
pkgdesc="A blogging library for KDE"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kxmlrpcclient syndication kcalcore)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d2c5be2461221d3228f59b085654afb8e686f8bbd162001c68789234c6dc1da2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
