# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kblog
pkgver=16.07.80
pkgrel=1
pkgdesc="A blogging library for KDE"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kxmlrpcclient kde-syndication kcalcore)
makedepends=(extra-cmake-modules python kdoctools kdesignerplugin)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('0950e4bbbc4e6fc0d8642b2f5c1310aa')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
