# Maintainer: Florian Maunier <fmauneko@dissidence.ovh>
# Contributor: jtmb <packaging at technologicalwizardry dot com>

pkgname=msbuild
pkgver=16.4+xamarinxplat.2019.09.09.15.03
pkgrel=2
pkgdesc="Xamarin implementation of the Microsoft build system"
url="https://github.com/mono/msbuild"
arch=('x86_64')
license=('MIT')
depends=('mono>=6.4.0')
makedepends=('unzip' 'dotnet-host>=3.0.0' 'dotnet-sdk>=3.0.0')
source=(https://download.mono-project.com/sources/msbuild/msbuild-${pkgver}.tar.xz
        copy_hostfxr.patch
        fix_bashisms.patch
        license_check_is_case_sensitive.diff
        case_sensitive_dotnetbits.patch)
sha256sums=('84c2e6021c43ba9dacc530d3d1bd6ed38e4f6b088a5e014f0ac4225843895d28'
            '041f536fbe5a36ba5bf7164460f07bd2becf4ecf2935594bf92676198b7becc2'
            'a13ecb4125c673372d87a3b7d957fc8716a3c3e74cd08e9e354b5dcf170ed453'
            '3a12a9c33ad5938e8af24d2985241053602f4efc94a4818a00a17da32ce4aba5'
            'aaa827b13abfb150572915d3daaa6b7140a7b4c12cda48aa76fed0f830a8bee1')

prepare() {
    cd ${pkgname}-${pkgver%+*}
    patch -Np1 < "${srcdir}/fix_bashisms.patch"
    patch -Np1 < "${srcdir}/copy_hostfxr.patch"
    patch -Np1 < "${srcdir}/case_sensitive_dotnetbits.patch"
    #patch --forward --strip=1 --input="${srcdir}/license_check_is_case_sensitive.diff"
	# dotnet RepositoryValidation requires lowercased
	ln -sf LICENSE license
}

build() {
    cd ${pkgname}-${pkgver%+*}
    DOTNET_MSBUILD_SDK_RESOLVER_CLI_DIR=/usr/share/dotnet \
		./eng/cibuild_bootstrapped_msbuild.sh \
		--host_type mono \
		--configuration Release \
		--skip_tests /p:DisableNerdbankVersioning=true

    ./stage1/mono-msbuild/msbuild mono/build/install.proj \
		/p:MonoInstallPrefix="$srcdir/target/usr" \
		/p:Configuration=Release-MONO \
		/p:IgnoreDiffFailure=true

    sed "s|${srcdir}/target||g" -i "$srcdir/target/usr/bin/msbuild"
	#find "$srcdir/target/usr/lib/mono/" -name Microsoft.DiaSymReader.Native.*dll -delete
    #find "$srcdir/target/usr/lib/mono/" -name *.dylib -delete
	#find "$srcdir/target/usr/lib/mono/" -name *.so -delete
}

package_msbuild() {
    cd ${pkgname}-${pkgver%+*}
    cp -dr --no-preserve=ownership "$srcdir/target/usr" "${pkgdir}"
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
